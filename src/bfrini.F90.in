!> @file
!> @brief Initialize global variables and arrays within internal memory.
!> @authors J. Woollen J. Ator @date 1994-01-06

!> Initialize numerous global variables and arrays
!> within internal modules and COMMON blocks throughout the NCEPLIBS-bufr
!> software.
!>
!> This subroutine isn't normally called directly by any application
!> program, because it's automatically called internally from within
!> subroutine openbf() during the first time that subroutine is called
!> by any application program.
!>
!> @authors J. Woollen J. Ator @date 1994-01-06
subroutine bfrini

use modv_vars, only: maxjl, maxtba, maxtbb, maxtbd, mxmsgl, nfiles

use moda_stbfr
use moda_idrdm
use moda_msglim
use moda_bitbuf
use moda_bufrmg
use moda_bufrsr
use moda_tababd
use moda_usrint
use moda_tables
use moda_h4wlc
use moda_dscach

COMMON /MAXCMP/ MAXCMB,MAXROW,MAXCOL,NCMSGS,NCSUBS,NCBYTS
COMMON /PADESC/ IBCT,IPD1,IPD2,IPD3,IPD4
COMMON /REPTAB/ IDNR(5,2),TYPS(5,2),REPS(5,2),LENS(5)
COMMON /DXTAB / MAXDX,IDXV,NXSTR(10),LDXA(10),LDXB(10),LDXD(10), &
                LD30(10),DXSTR(10)
COMMON /MRGCOM/ NRPL,NMRG,NAMB,NTOT
COMMON /DATELN/ LENDAT
COMMON /ACMODE/ IAC
COMMON /MSGSTD/ CSMF
COMMON /MSGCMP/ CCMF
COMMON /TABLEF/ CDMF
COMMON /TNKRCP/ ITRYR,ITRMO,ITRDY,ITRHR,ITRMI,CTRT

character*240 CMTDIR
character*56  DXSTR
character*6   ADSN(5,2),DNDX(25,10)
character*3   TYPX(5,2),TYPS
character*1   REPX(5,2),REPS
character*1   CSMF
character*1   CCMF
character*1   CDMF
character*1   CTRT
dimension     NDNDX(10),NLDXA(10),NLDXB(10),NLDXD(10),NLD30(10)
dimension     LENX(5)

data ADSN   / '101000','360001','360002','360003','360004' , &
              '101255','031002','031001','031001','031000' /
data TYPX   /    'REP',   'DRP',   'DRP',   'DRS' ,  'DRB' , &
                 'SEQ',   'RPC',   'RPC',   'RPS' ,  'SEQ' /
data REPX   /      '"',     '(',     '{',     '[' ,    '<' , &
                   '"',     ')',     '}',     ']' ,    '>' /
data LENX   /       0 ,     16 ,      8 ,      8  ,     1  /

data (DNDX(I,1),I=1,25)/ &
'102000','031001','000001','000002', &
'110000','031001','000010','000011','000012','000013','000015', &
                  '000016','000017','000018','000019','000020', &
'107000','031001','000010','000011','000012','000013','101000', &
                  '031001','000030'/

data (DNDX(I,2),I=1,15)/ &
'103000','031001','000001','000002','000003', &
'101000','031001','300004', &
'105000','031001','300003','205064','101000','031001','000030'/

data NDNDX /  25 ,  15 , 8*0 /
data NLDXA /  35 ,  67 , 8*0 /
data NLDXB /  80 , 112 , 8*0 /
data NLDXD /  38 ,  70 , 8*0 /
data NLD30 /   5 ,   6 , 8*0 /

!-----------------------------------------------------------------------
!-----------------------------------------------------------------------

!  Initialize module moda_bitbuf

maxbyt = min(10000,mxmsgl)

!  Initialize module moda_h4wlc

nh4wlc = 0

!  Initialize /MAXCMP/

MAXCMB = MAXBYT
MAXROW = 0
MAXCOL = 0
NCMSGS = 0
NCSUBS = 0
NCBYTS = 0

!  Initialize /PADESC/

IBCT = ifxy('063000')
IPD1 = ifxy('102000')
IPD2 = ifxy('031001')
IPD3 = ifxy('206001')
IPD4 = ifxy('063255')

!  Initialize module moda_stbfr

do i=1,nfiles
  iolun(i) = 0
  iomsg(i) = 0
enddo

!  Initialize module moda_idrdm

do i=1,nfiles
  idrdm(i) = 0
enddo

!  Initialize module moda_msglim

do i=1,nfiles
  msglim(i) = 3
enddo

!  Initialize module moda_usrint

do i=1,nfiles
  nval(i) = 0
enddo

!  Initialize /REPTAB/

do I=1,5
  LENS(I) = LENX(I)
  do J=1,2
    IDNR(I,J) = ifxy(ADSN(I,J))
    TYPS(I,J) = TYPX(I,J)
    REPS(I,J) = REPX(I,J)
  enddo
enddo

!  Initialize module moda_tababd

!    ntba(0) is the maximum number of entries within internal BUFR table A
ntba(0) = maxtba
!    ntbb(0) is the maximum number of entries within internal BUFR Table B
ntbb(0) = maxtbb
!    ntbd(0) is the maximum number of entries within internal BUFR Table D
ntbd(0) = maxtbd

!  Initialize /DXTAB/

MAXDX = maxbyt
!  .... IDXV is the version number of the local tables
IDXV  = 1

do J=1,10
  LDXA(J)  = NLDXA(J)
  LDXB(J)  = NLDXB(J)
  LDXD(J)  = NLDXD(J)
  LD30(J)  = NLD30(J)
  DXSTR(J) = '      '
  NXSTR(J) = NDNDX(J)*2
  do I=1,NDNDX(J)
    I1 = I*2-1
    call ipkm(DXSTR(J)(I1:I1),2,ifxy(DNDX(I,J)))
  enddo
enddo

!  Initialize module moda_tables

maxtab = maxjl

!  Initialize module moda_bufrmg

do i=1,nfiles
  msglen(i) = 0
enddo

!  Initialize /MRGCOM/

NRPL = 0
NMRG = 0
NAMB = 0
NTOT = 0

!  Initialize /DATELN/

if(LENDAT.ne.10) LENDAT = 8

!  Initialize /ACMODE/

IAC = 0

!  Initialize module moda_bufrsr

do i=1,nfiles
  jsr(i) = 0
enddo

!  Initialize module moda_dscach

ncnem = 0

!  Initialize /MSGSTD/

CSMF = 'N'

!  Initialize /MSGCMP/

CCMF = 'N'

!  Initialize /TABLEF/

CDMF = 'N'

!  Initialize /TNKRCP/

CTRT = 'N'

!  Initialize /MSTINF/

CMTDIR = &
'@MASTER_TABLE_DIR_F@'
call mtinfo(CMTDIR,98,99)

return
end
